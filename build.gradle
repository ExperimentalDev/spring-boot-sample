buildscript {
    ext {
        springBootVersion = "1.5.4.RELEASE"
        junitVersion = "4.12"
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
    repositories {
        mavenCentral()
    }
}

apply plugin: "jacoco"

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'jacoco'

    group = 'org.expdev.sample'
    version = '0.0.1'

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "http://dl.bintray.com/typesafe/maven-releases" }
    }

    configurations {
        codacy
    }

    dependencies {
        codacy group: 'com.codacy', name: 'codacy-coverage-reporter', version: '2.0.0'
        testCompile "junit:junit:${junitVersion}"
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    bootRepackage.enabled = false
    bootRun.enabled = false

    jacocoTestReport {
        reports {
            xml.enabled true
            csv.enabled false
        }
    }

    task uploadCoverageToCodacy(type: JavaExec, dependsOn : jacocoTestReport) {
        main = "com.codacy.CodacyCoverageReporter"
        classpath = configurations.codacy
        args = [
                "-l",
                "Java",
                "-r",
                "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        ]
    }

    task (codacyDepsize) << {
        def size = 0;
        configurations.codacy.collect { it.length() / (1024 * 1024) }.each { size += it }
        println "Total dependencies size: ${Math.round(size * 100) / 100} Mb"

        configurations
                .codacy
                .sort { -it.length() }
                .each { println "${it.name} : ${Math.round(it.length() / (1024) * 100) / 100} kb" }
    }

    task (codacyLocs) << {
        configurations.codacy.each {
            String jarName = it
            println jarName
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}



